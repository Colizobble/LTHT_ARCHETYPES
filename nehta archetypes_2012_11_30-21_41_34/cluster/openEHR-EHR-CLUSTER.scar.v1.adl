archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.scar.v1

concept
	[at0000]	-- Examination of Scar
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2012-11-01">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording the examination findings from the physical examination of a single scar in the skin.">
			use = <"Use to record the examination findings from the physical examination of a single scar in the skin at an identified anatomical location. 

If multiple scars are observed during examination at the same identified anatomical location, each should be recorded separately.

This archetype is designed to be used in SLOTs within clinically appropriate OBSERVATION or other CLUSTER archetypes as part of recording the findings on clinical examination - for example nested within OBSERVATION.exam or CLUSTER.skin.">
			keywords = <"scar", "examination", "inspection", "skin", "wound", "healing", "keloid", "hypertrophic">
			misuse = <"">
			copyright = <"© National E-Health Transition Authority">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <"Kerrie Lee", ...>
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, Australia, heather.leslie@oceaninformatics.com">
		["references"] = <"Direct communications with clinical domain experts.">
		["MD5-CAM-1.0.1"] = <"CE55D3B080FB9405EB3DCE6517EA7189">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of Scar
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0009] matches {	-- Anatomical Location
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Presence of Scar?
				value matches {
					DV_BOOLEAN matches {
						value matches {False}
					}
				}
			}
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Clinical Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {	-- Healing
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0005, 	-- Normal
							at0007, 	-- Hypertrophic
							at0006, 	-- Keloid
							at0008]	-- Atrophic
						}
					}
				}
			}
			allow_archetype CLUSTER[at0011] occurrences matches {0..*} matches {	-- Details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.dimensions(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0002] occurrences matches {0..*} matches {	-- Clinical Interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0010] occurrences matches {0..*} matches {	-- Multimedia Representation
				value matches {
					DV_MULTIMEDIA matches {
						media_type matches {
							[openEHR::
							425, 
							426, 
							427, 
							428, 
							429, 
							401, 
							402, 
							404, 
							405, 
							406, 
							407, 
							414]
						}
					}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of Scar">
					description = <"Physical examination of a single scar resulting from repair of a wound or injury in the skin.">
				>
				["at0001"] = <
					text = <"Clinical Description">
					description = <"Narrative description of the scar.">
					comment = <"For example, the extent and possible cause; colour and associated pigmentation; shape and texture .">
				>
				["at0002"] = <
					text = <"Clinical Interpretation">
					description = <"Single word, phrase or brief summary description of the scar.">
					comment = <"For example, the ">
				>
				["at0003"] = <
					text = <"Presence of Scar?">
					description = <"Is a scar present in the anatomical location being examined?">
					comment = <"Record as true if a scar is identified at the identified anatomical location.">
				>
				["at0004"] = <
					text = <"Healing">
					description = <"Description of the healing that has occurred.">
				>
				["at0005"] = <
					text = <"Normal">
					description = <"The scar appears to have healed normally.">
				>
				["at0006"] = <
					text = <"Keloid">
					description = <"The scar has healed with marked hypertrophy, with growth evident outside the original wound area.">
				>
				["at0007"] = <
					text = <"Hypertrophic">
					description = <"The scar is raised above the surrounding skin.">
				>
				["at0008"] = <
					text = <"Atrophic">
					description = <"The scar is depressed below the surrounding skin.">
				>
				["at0009"] = <
					text = <"Anatomical Location">
					description = <"Identification of an anatomical location of the scar being examined.">
					comment = <"Coding with a terminology is desirable, where possible. For example, mid-sternum, or right iliac fossa. If this archetype is already nested within an archetype where the appropriate level of detail for identification of the anatomical location has already been specified, this data element may be regarded as redundant, or the system may automatically insert the same anatomical location here.">
				>
				["at0010"] = <
					text = <"Multimedia Representation">
					description = <"Digital image, video or diagram of the scar.">
				>
				["at0011"] = <
					text = <"Details">
					description = <"Detailed structured examination findings.">
					comment = <"For example, further details about the precise size or percentage of the body or limb covered by the scar.">
				>
				["at0012"] = <
					text = <"Comment">
					description = <"Additional narrative comment about the examination, not captured in other structured data fields.">
				>
			>
		>
	>
